;==============================================================================
;
; Title:		CollectionOfStructs.dbl
;
; Description:	This program demonstrates the creation and use of a collection
;				of structure fields. Collections and structure fields were both
;				new features of Synergy/DE introduced in V9.1.1.
;
; Author:		Steve Ives (Synergex Professional Services Group)
;
;==============================================================================
;
.ifdef DBLV915
import Synergex.SynergyDE.Collections	;New in V9.1.5, 1-based
.else
import System.Collections				;1-based prior to V9.1.5, now 0-based
.endc

structure strErrorMessage			;This structure matches the record layout
	Facility	,a3					;of DBLDIR:syntxt.ism
	Number		,d5
	Code		,a10
	Severity	,a1
	Message		,a199
endstructure

structure strMsgOut					;Format for output to screen
	Fac			,a3
				,a1
	Nbr			,a5
				,a1
	Cd			,a10
				,a1
	Msg			,a30
endstructure

main CollectionOfStructs

record 
	CompilerMessage		,strErrorMessage	;Struct field for one message
	CompilerMessages	,@ArrayList			;Collection of messages
	MSgOut				,strMsgOut			;Screen output buffer

	ChanTt		,i4		,1				;Terminal channel
	ChanFile	,i4		,2				;File channel
	idx			,i4						;Loop counter
endrecord


proc

	open(ChanTt,i,"tt:")
	open(ChanFile,i:i,"DBLDIR:syntxt.ism")

	CompilerMessages = new ArrayList()

	;Position to the first compiler message
	find(ChanFile,,"CMP")
	
	;Load the compiler messages into the collection
	repeat
	begin
		reads(ChanFile,CompilerMessage,eof)
		if (CompilerMessage.Facility!="CMP")
			exitloop
		;Box the record and add it to the collection
		CompilerMessages.Add((@strErrorMessage)CompilerMessage)
	end
eof,
	
	;Now lets run through the collection, display each item
	for idx from 1 thru CompilerMessages.Count
	begin
		MsgOut.Fac		= ((strErrorMessage)CompilerMessages[idx]).Facility
		MsgOut.Nbr		= ((strErrorMessage)CompilerMessages[idx]).Number
		MsgOut.Cd		= ((strErrorMessage)CompilerMessages[idx]).Code
		MsgOut.Msg		= ((strErrorMessage)CompilerMessages[idx]).Message
		writes(ChanTt,MsgOut)
	end
	
	;Display how many records we found
	writes(ChanTt,string(CompilerMessages.Count)+" compiler messages found")
	
	close ChanFile
	close ChanTt
	
	stop

endmain
